// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {IJBCashOutHook} from "@bananapus/core-v6/src/interfaces/IJBCashOutHook.sol";
import {IJBDirectory} from "@bananapus/core-v6/src/interfaces/IJBDirectory.sol";
import {IJBPayHook} from "@bananapus/core-v6/src/interfaces/IJBPayHook.sol";
import {IJBRulesetDataHook} from "@bananapus/core-v6/src/interfaces/IJBRulesetDataHook.sol";

import {StickyTranche} from "../structs/StickyTranche.sol";

/// @notice A data hook that tracks staking positions for sticky projects: per-epoch tranches, LIFO unstaking, net
/// stake buckets by joining epoch, and a holder-level streak clock.
interface IStickyHook is IJBRulesetDataHook, IJBPayHook, IJBCashOutHook {
    /// @notice Emitted when backing present without any shares is excluded from future holders' claims.
    /// @param projectId The ID of the sticky project.
    /// @param amount The total excluded underlying balance, in underlying token atoms.
    /// @param caller The terminal that recorded the first payment of the new share supply.
    event ExcludeOrphanedBalance(uint256 indexed projectId, uint256 amount, address caller);

    /// @notice Emitted when an address is allowed to airdrop stakes to any holder of a sticky project.
    /// @param projectId The ID of the sticky project the sender can airdrop to.
    /// @param granter The address allowed to airdrop.
    /// @param caller The address that set the granter.
    event SetGranter(uint256 indexed projectId, address indexed granter, address caller);

    /// @notice Emitted when a sticky project's share token is registered as its movement reporter.
    /// @param projectId The ID of the sticky project.
    /// @param token The Sticky share token allowed to report transfers and burns.
    /// @param caller The address that registered the token.
    event SetToken(uint256 indexed projectId, address token, address caller);

    /// @notice Emitted when a holder allows or disallows a sender to add stakes to their position.
    /// @param projectId The ID of the sticky project the trust applies to.
    /// @param holder The holder whose position the sender can add to.
    /// @param sender The sender being trusted or untrusted.
    /// @param trusted Whether the sender is now trusted.
    event SetTrustedSender(uint256 indexed projectId, address indexed holder, address indexed sender, bool trusted);

    /// @notice Emitted when Sticky shares join the holder's newest tranche of the epoch or create a new one.
    /// @param projectId The ID of the sticky project being staked to.
    /// @param holder The address the staked position belongs to.
    /// @param payer The payment payer or transfer sender whose shares joined the position.
    /// @param count The shares added by a payment or incoming transfer, as a fixed point number with 18 decimals.
    /// @param stakedBalance The holder's staked balance after the stake, as a fixed point number with 18 decimals.
    /// @param caller The address that triggered the stake.
    event Staked(
        uint256 indexed projectId,
        address indexed holder,
        address payer,
        uint256 count,
        uint256 stakedBalance,
        address caller
    );

    /// @notice Emitted when a holder's streak ends because their staked balance reached zero.
    /// @param projectId The ID of the sticky project the streak belongs to.
    /// @param holder The address whose streak ended.
    /// @param duration The number of seconds the streak lasted.
    /// @param caller The address that triggered the unstake which ended the streak.
    event StreakEnded(uint256 indexed projectId, address indexed holder, uint256 duration, address caller);

    /// @notice Emitted when a holder's streak starts because their staked balance became non-zero.
    /// @param projectId The ID of the sticky project the streak belongs to.
    /// @param holder The address whose streak started.
    /// @param caller The address that triggered the stake which started the streak.
    event StreakStarted(uint256 indexed projectId, address indexed holder, address caller);

    /// @notice Emitted when Sticky shares burn or transfer away, consuming tranches newest-first.
    /// @dev A voluntary burn or transfer does not reclaim backing; this event alone does not prove a cash out.
    /// @param projectId The ID of the sticky project being unstaked from.
    /// @param holder The address the staked position belongs to.
    /// @param count The number of Sticky shares removed, as a fixed point number with 18 decimals.
    /// @param stakedBalance The holder's staked balance after the unstake, as a fixed point number with 18 decimals.
    /// @param caller The registered Sticky share token that reported the movement.
    event Unstaked(
        uint256 indexed projectId, address indexed holder, uint256 count, uint256 stakedBalance, address caller
    );

    /// @notice The address allowed to set a project's granters, once, at launch.
    /// @return deployer The immutable deployer.
    function DEPLOYER() external view returns (address deployer);

    /// @notice The directory of terminals and controllers for projects.
    /// @return directory The directory used to validate terminal callbacks.
    function DIRECTORY() external view returns (IJBDirectory directory);

    /// @notice The duration of one stake-age epoch. Tranches created in the same epoch merge, and net stake is
    /// bucketed by the epoch it joined in.
    /// @return duration The epoch duration, in seconds.
    function EPOCH_DURATION() external view returns (uint256 duration);

    /// @notice The duration of a holder's active streak, in seconds.
    /// @param projectId The ID of the sticky project to check the streak of.
    /// @param holder The address to check the streak of.
    /// @return duration The number of seconds since the holder's staked balance last became non-zero, or 0 if nothing
    /// is staked.
    function currentStreakOf(uint256 projectId, address holder) external view returns (uint256 duration);

    /// @notice Whether an address can airdrop stakes to any holder of a sticky project.
    /// @param projectId The ID of the sticky project to check.
    /// @param granter The address to check.
    /// @return isGranter Whether the address is a project granter.
    function isGranterOf(uint256 projectId, address granter) external view returns (bool isGranter);

    /// @notice Whether a payment to a project has minted shares this hook has not yet recorded.
    /// @dev Set by the registered share token on every mint and cleared by the terminal's after-pay callback, so the
    /// distributor never reads a tenure denominator while the epoch buckets lag the share supply.
    /// @param projectId The ID of the sticky project to check.
    /// @return isPaying Whether a payment's minted shares are still waiting for their tranche.
    function isPayingFor(uint256 projectId) external view returns (bool isPaying);

    /// @notice Whether a holder allows a sender to add stakes to their position.
    /// @param projectId The ID of the sticky project to check.
    /// @param holder The holder whose position would be added to.
    /// @param sender The sender to check.
    /// @return isTrusted Whether the holder trusts the sender.
    function isTrustedSenderOf(uint256 projectId, address holder, address sender) external view returns (bool isTrusted);

    /// @notice The longest streak a holder has ever had, including their active streak.
    /// @param projectId The ID of the sticky project to check the streak of.
    /// @param holder The address to check the streak of.
    /// @return duration The holder's longest streak duration, in seconds.
    function longestStreakOf(uint256 projectId, address holder) external view returns (uint256 duration);

    /// @notice The net stake still held from tranches created in an epoch, as a fixed point number with 18 decimals.
    /// @param projectId The ID of the sticky project.
    /// @param epoch The epoch, measured as `timestamp / EPOCH_DURATION`.
    /// @return amount The epoch's net stake bucket.
    function netStakedIn(uint256 projectId, uint256 epoch) external view returns (uint256 amount);

    /// @notice The net stake still held from tranches created within an inclusive epoch range, as a fixed point
    /// number with 18 decimals.
    /// @param projectId The ID of the sticky project.
    /// @param fromEpoch The first epoch to include.
    /// @param toEpoch The last epoch to include.
    /// @return amount The sum of the range's net stake buckets.
    function netStakedWithin(
        uint256 projectId,
        uint256 fromEpoch,
        uint256 toEpoch
    )
        external
        view
        returns (uint256 amount);

    /// @notice Backing excluded when the most recent share supply began.
    /// @dev While no shares exist, all current terminal backing is unowned even if this stored value is lower.
    /// @param projectId The ID of the sticky project.
    /// @return amount The excluded underlying balance, in underlying token atoms.
    function orphanedBalanceOf(uint256 projectId) external view returns (uint256 amount);

    /// @notice The total number of Sticky shares in a holder's position, as a fixed point number with 18 decimals.
    /// @param projectId The ID of the sticky project to check the balance of.
    /// @param holder The address to check the balance of.
    /// @return balance The holder's current Sticky share balance.
    function stakedBalanceOf(uint256 projectId, address holder) external view returns (uint256 balance);

    /// @notice A holder's staked balance held in tranches created through an epoch, as a fixed point number with 18
    /// decimals.
    /// @param projectId The ID of the sticky project.
    /// @param holder The holder whose tranches to read.
    /// @param epoch The last epoch to include.
    /// @return balance The staked balance from tranches created in or before the epoch.
    function stakedBalanceThroughEpochOf(
        uint256 projectId,
        address holder,
        uint256 epoch
    )
        external
        view
        returns (uint256 balance);

    /// @notice The timestamp at which a holder's active streak started, or 0 if nothing is staked.
    /// @param projectId The ID of the sticky project to check the streak of.
    /// @param holder The address to check the streak of.
    /// @return timestamp The active streak's start timestamp, or zero if the balance is zero.
    function streakStartOf(uint256 projectId, address holder) external view returns (uint256 timestamp);

    /// @notice The Sticky share token allowed to report transfers and burns for a project.
    /// @param projectId The ID of the sticky project to get the token of.
    /// @return token The project's registered Sticky share token.
    function tokenOf(uint256 projectId) external view returns (address token);

    /// @notice The number of tranches a holder has.
    /// @param projectId The ID of the sticky project to check the tranches of.
    /// @param holder The address to check the tranches of.
    /// @return count The number of active tranches.
    function trancheCountOf(uint256 projectId, address holder) external view returns (uint256 count);

    /// @notice A holder's tranches, oldest first.
    /// @dev Copies every active tranche. Use the paginated overload for positions with many deposits or transfers.
    /// @param projectId The ID of the sticky project to get the tranches of.
    /// @param holder The address to get the tranches of.
    /// @return tranches The active tranches, oldest first.
    function tranchesOf(uint256 projectId, address holder) external view returns (StickyTranche[] memory tranches);

    /// @notice A bounded range of a holder's active tranches, oldest first.
    /// @param projectId The ID of the sticky project.
    /// @param holder The holder whose tranches to read.
    /// @param start The zero-based index of the first tranche to read.
    /// @param count The maximum number of tranches to return, capped at 256.
    /// @return tranches The requested tranches, ending at the active count if fewer remain.
    function tranchesOf(
        uint256 projectId,
        address holder,
        uint256 start,
        uint256 count
    )
        external
        view
        returns (StickyTranche[] memory tranches);

    /// @notice Consumes the newest tranches for every positive Sticky share burn, including voluntary controller burns.
    /// @dev Only the registered Sticky share token can report burns. Zero burns leave accounting unchanged.
    /// @param projectId The ID of the sticky project.
    /// @param holder The holder whose shares were burned.
    /// @param amount The number of shares burned, as a fixed point number with 18 decimals.
    function recordBurn(uint256 projectId, address holder, uint256 amount) external;

    /// @notice Counts a share issuance whose tranche this hook has not yet recorded, flagging the project's payment as
    /// in progress until the terminal's after-pay callback records it.
    /// @dev Only the registered Sticky share token can report mints.
    /// @param projectId The ID of the sticky project whose shares were issued.
    function recordMint(uint256 projectId) external;

    /// @notice Moves staked accounting between holders for a transferable Sticky share token: the sender's newest
    /// tranches are consumed and the moved shares join the receiver's newest tranche of the current epoch, or a fresh
    /// one. The receiver's existing streak continues.
    /// @dev Can only be called by the project's registered Sticky share token. Zero and self transfers are no-ops.
    /// @param projectId The ID of the sticky project the transfer belongs to.
    /// @param from The holder the shares moved from.
    /// @param to The holder the shares moved to.
    /// @param amount The number of shares moved, as a fixed point number with 18 decimals.
    function recordTransfer(uint256 projectId, address from, address to, uint256 amount) external;

    /// @notice Allows addresses to airdrop stakes to any holder of a sticky project.
    /// @dev Can only be called by the deployer, which calls it once at launch.
    /// @param projectId The ID of the sticky project the senders can airdrop to.
    /// @param granters The addresses allowed to airdrop.
    function setGrantersFor(uint256 projectId, address[] calldata granters) external;

    /// @notice Registers the Sticky share token allowed to report transfers and burns for a project.
    /// @dev Can only be called by the deployer, which calls it once at launch.
    /// @param projectId The ID of the sticky project.
    /// @param token The Sticky share token.
    function setTokenFor(uint256 projectId, address token) external;

    /// @notice Allows or disallows a sender to add stakes to the caller's position.
    /// @param projectId The ID of the sticky project the trust applies to.
    /// @param sender The sender to trust or untrust.
    /// @param trusted Whether the sender should be trusted.
    function setTrustedSenderFor(uint256 projectId, address sender, bool trusted) external;
}
